/*
* generated by Xtext
*/
package xtraitj.ui.labeling

import com.google.inject.Inject
import org.eclipse.emf.edit.ui.provider.AdapterFactoryLabelProvider
import org.eclipse.jdt.ui.JavaElementImageDescriptor
import org.eclipse.jface.resource.ImageDescriptor
import org.eclipse.jface.viewers.StyledString
import org.eclipse.xtext.common.types.JvmOperation
import org.eclipse.xtext.common.types.JvmVisibility
import org.eclipse.xtext.xbase.ui.labeling.XbaseImages2
import org.eclipse.xtext.xbase.ui.labeling.XbaseLabelProvider
import xtraitj.util.XtraitjAnnotatedElementHelper
import xtraitj.xtraitj.TJClass
import xtraitj.xtraitj.TJConstructor
import xtraitj.xtraitj.TJField
import xtraitj.xtraitj.TJMethod
import xtraitj.xtraitj.TJMethodDeclaration
import xtraitj.xtraitj.TJRequiredMethod
import xtraitj.xtraitj.TJTrait
import xtraitj.xtraitj.TJTraitReference

import static extension xtraitj.util.XtraitjModelUtil.*

/**
 * Provides labels for a EObjects.
 * 
 * see http://www.eclipse.org/Xtext/documentation.html#labelProvider
 */
class XtraitjLabelProvider extends XbaseLabelProvider {
	
	@Inject XbaseImages2 images
	@Inject extension XtraitjAnnotatedElementHelper

	@Inject
	new(AdapterFactoryLabelProvider delegate) {
		super(delegate);
	}

	def text(TJMethodDeclaration m) {
		new StyledString(
			m.name + m.parameterRepresentation
		).append(
			new StyledString(" : " + 
				m.type.simpleName,
				StyledString::DECORATIONS_STYLER
			)
		);
	}

	def text(TJConstructor cons) {
		cons.constructorRepresentation
	}

	def text(TJField f) {
		f.name + " : " + f.getType().getSimpleName()
	}
	
	def text(TJTraitReference r) {
		r.trait.simpleName
	}
	
	def image(TJTrait t) {
		"trait_obj.gif"
	}
	
	def image(TJClass c) {
		"class_obj.gif"
	}
	
	def image(TJField f) {
		imageForRequiredField();
	}
	
	private def imageForRequiredField() {
		images.forField(JvmVisibility::PRIVATE, 0)
	}
	
	protected override ImageDescriptor _imageDescriptor(JvmOperation operation) {
		if (operation.annotatedRequiredField) {
			imageForRequiredField()
		} else if (operation.annotatedDefinedMethod) {
			images.forOperation(JvmVisibility.PUBLIC, 0)
		} else {
			super._imageDescriptor(operation)
		}
	}

	def image(TJMethod m) {
		images.forOperation(
			if (m.private)
				JvmVisibility.PRIVATE
			else
				JvmVisibility.PUBLIC
			, 
			0);
	}

	def image(TJConstructor c) {
		images.forConstructor(JvmVisibility::PUBLIC, 0)
	}
	
	def image(TJRequiredMethod m) {
		images.forOperation(
			JvmVisibility::PUBLIC, 
			JavaElementImageDescriptor::ABSTRACT);
	}
	
	def image(TJTraitReference r) {
		if (!r.operations.empty)
			return "trait_altered.gif"
		return "trait_ref.gif"
	}
	
}
