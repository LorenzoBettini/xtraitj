grammar xtraitj.Xtraitj with org.eclipse.xtext.xbase.annotations.XbaseWithAnnotations

import "http://www.eclipse.org/xtext/common/JavaVMTypes" as types

generate xtraitj "http://www.Xtraitj.xtraitj"

TJProgram:
	('package' name=QualifiedName ';')?
	importSection=XImportSection?
	elements+=TJDeclaration*
;

TJDeclaration:
	TJTrait | TJClass
;

TJTrait:
	'trait' name=ID
	('<' traitTypeParameters+=JvmTypeParameter (',' traitTypeParameters+=JvmTypeParameter)* '>')? 
	('uses' traitExpression=TJTraitExpression)? '{'
		members+=TJMember*
	'}'
;

TJClass:
	'class' name=ID 
		('<' classTypeParameters+=JvmTypeParameter (',' classTypeParameters+=JvmTypeParameter)* '>')? 
			('implements'
				interfaces += JvmParameterizedTypeReference
				(',' interfaces += JvmParameterizedTypeReference)*
			)?
			('uses' traitExpression=TJTraitExpression)? '{'
		fields+=TJField*
		constructors+=TJConstructor*
	'}'
;

TJTraitExpression:
	references += TJTraitReference (
		','
		references += TJTraitReference)*
;

TJTraitReference: 	
	trait=JvmParameterizedTypeReference // [TJTrait|QualifiedName]
	//('<' arguments+=JvmArgumentTypeReference (',' arguments+=JvmArgumentTypeReference)* '>')?
	('['
		operations+=TJTraitOperation
		(',' operations+=TJTraitOperation)*
	']')?
;

TJTraitOperation:
	TjTraitOperationForProvided |
	TJRenameOperation |
	TJRedirectOperation
;

TjTraitOperationForProvided:
	TJHideOperation | TJRestrictOperation | TJAliasOperation
;

TJHideOperation: {TJHideOperation} 'hide' member=[types::JvmMember] ;

TJRestrictOperation: {TJRestrictOperation} 'restrict' member=[types::JvmMember] ;

TJAliasOperation: {TJAliasOperation} 'alias' member=[types::JvmMember] 'as' newname=ID ;

TJRenameOperation: {TJRenameOperation} 'rename' member=[types::JvmMember] 'to' newname=ID ;

TJRedirectOperation: {TJRedirectOperation} 'redirect' member=[types::JvmMember] 'to' member2=[types::JvmMember] ;

TJMember:
	=>TJField | TJMethodDeclaration;

TJField:
	annotations+=XAnnotation*
	type=JvmTypeReference name=ValidID ('=' init=XExpression)? ';' ;

TJMethodDeclaration:
	=>TJRequiredMethod | TJMethod
;

TJRequiredMethod:
	('<' typeParameters+=JvmTypeParameter (',' typeParameters+=JvmTypeParameter)* '>')?
	type=JvmTypeReference name=ValidID 
		'(' (params+=FullJvmFormalParameter (',' params+=FullJvmFormalParameter)*)? ')'
		';' ;

TJMethod:
	annotations+=XAnnotation*
	private?='private'? 
		('<' typeParameters+=JvmTypeParameter (',' typeParameters+=JvmTypeParameter)* '>')?
		type=JvmTypeReference name=ValidID 
		'(' (params+=FullJvmFormalParameter (',' params+=FullJvmFormalParameter)*)? ')' 
		body=XBlockExpression;

TJConstructor:
	name=ValidID 
		'(' (params+=FullJvmFormalParameter (',' params+=FullJvmFormalParameter)*)? ')' 
		body=XBlockExpression;
